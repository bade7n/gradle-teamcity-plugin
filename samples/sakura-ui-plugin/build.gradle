
plugins {
    id 'java'
    id 'com.github.rodm.teamcity-server' version '1.4'
    id 'com.github.rodm.teamcity-environments' version '1.4'
}

group = 'com.github.rodm.teamcity'
version = '1.0-SNAPSHOT'

ext {
    vendorName = 'rodm'
    teamcityVersion = '2020.2'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java11Home = project.findProperty('java11.home') ?: '/opt/jdk-11.0.2'
}

def dockerCommandLine = ['docker', 'run', '--rm',
                         '--name', 'npmbuild',
                         '-v', "${projectDir}:/project",
                         '-w', '/project', 'node:lts']

task npmInstall(type: Exec) {
    commandLine = [*dockerCommandLine, 'npm', 'install']
}

task webpack(type: Exec) {
    outputs.cacheIf { true }
    inputs.dir('src/main/js')
        .withPropertyName('js')
        .withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.files('package-lock.json', 'webpack.config.js')
        .withPropertyName('configFiles')
        .withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.dir("$buildDir/js")
        .withPropertyName('bundle')
    commandLine = [*dockerCommandLine, 'npm', 'run', 'build']
}

teamcity {
    version = teamcityVersion

    server {
        descriptor {
            name = project.name
            displayName = project.name
            version = project.version
            vendorName = project.vendorName
            vendorUrl = 'http://example.com'
            description = 'Example Sakura UI Plugin'

            downloadUrl = 'https://github.com/rodm/gradle-teamcity-plugin/'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }

        web {
            webpack
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        'teamcity2022.04' {
            version = '2022.04.1'
            javaHome = file(java11Home)
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }
    }
}
