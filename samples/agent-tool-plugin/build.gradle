
plugins {
    id 'com.github.rodm.teamcity-agent'
    id 'com.github.rodm.teamcity-server'
    id 'com.github.rodm.teamcity-environments'
}

group = 'com.github.rodm.teamcity'
version = '1.0-SNAPSHOT'

ext {
    teamcityVersion = '2020.1'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java11Home = project.findProperty('java11.home') ?: '/opt/jdk-11.0.2'
}

repositories {
    mavenCentral()
}

configurations {
    maven
}

dependencies {
    maven group: 'org.apache.maven', name: 'apache-maven', version: '3.5.0', classifier: 'bin', ext: 'zip'
}

agentPlugin {
    archiveBaseName = 'maven3_5'
    archiveVersion = ''
}

serverPlugin {
    archiveBaseName = 'maven3_5-tool'
    archiveVersion = ''
}

teamcity {
    version = teamcityVersion

    agent {
        descriptor {
            toolDeployment {
                executableFiles {
                    include 'bin/mvn'
                }
            }
        }

        files {
            from(zipTree(configurations.maven.singleFile)) {
                includeEmptyDirs = false
                eachFile { file ->
                    file.relativePath = new RelativePath(file.relativePath.endsWithFile, file.relativePath.segments[1..-1] as String[])
                }
            }
        }
    }

    server {
        descriptor {
            name = 'maven3_5-tool'
            displayName = 'Repacked Maven 3.5'
            version = project.version
            vendorName = 'rodm'
            vendorUrl = 'http://example.com'
            description = 'TeamCity Example Tool Plugin - Repacked Maven 3.5'
            downloadUrl = 'https://github.com/rodm/gradle-teamcity-plugin/'
            email = 'rod.n.mackenzie@gmail.com'
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        'teamcity2022.04' {
            version = '2022.04.1'
            javaHome = file(java11Home)
        }
    }
}
