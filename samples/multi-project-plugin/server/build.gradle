
plugins {
    id 'java'
    id 'com.github.rodm.teamcity-server'
    id 'com.github.rodm.teamcity-environments'
}

ext {
    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
    java11Home = project.findProperty('java11.home') ?: '/opt/jdk-11.0.2'
}

dependencies {
    implementation project(':common')
    agent project(path: ':agent', configuration: 'plugin')
}

teamcity {
    server {
        descriptor {
            name = 'Example TeamCity Plugin'
            displayName = 'Example TeamCity Plugin'
            version = project.version
            vendorName = 'rodm'
            vendorUrl = 'http://example.com'
            description = 'Example multi-project TeamCity plugin'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = "$rootDir/data"

        'teamcity2020.1' {
            version = '2020.1.5'
            javaHome = file(java8Home)
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }

        'teamcity2022.04' {
            version = '2022.04.1'
            javaHome = file(java11Home)
        }
    }
}
