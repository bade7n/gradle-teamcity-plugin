
plugins {
    id 'io.github.rodm.teamcity-environments' version '1.4'
}

group = 'com.github.rodm.teamcity'
version = '1.0-SNAPSHOT'

ext {
    vendorName = 'rodm'
    teamcityVersion = '2020.1'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_121'
    java11Home = project.findProperty('java11.home') ?: '/opt/jdk-11.0.2'
}

configurations {
    teamcityPlugins
}

dependencies {
    teamcityPlugins project(path: ':plugin1', configuration: 'plugin')
    teamcityPlugins project(path: ':plugin2', configuration: 'plugin')
}

teamcity {
    version = teamcityVersion

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        'teamcity2020.1' {
            version = '2020.1.5'
            javaHome = file(java8Home)
            plugins = configurations.teamcityPlugins
        }

        'teamcity2022.04' {
            version = '2022.04.1'
            javaHome = file(java11Home)
            plugins = configurations.teamcityPlugins
        }
    }
}
